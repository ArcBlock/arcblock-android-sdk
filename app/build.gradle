apply plugin: 'com.android.application'
apply plugin: 'com.apollographql.android'
apply plugin: 'jacoco'

jacoco {
  toolVersion = "0.7.1.201405082137"
}

version = new File(rootDir.getAbsolutePath() + '/version').text.replace("\n", "")

apollo {
  useJavaBeansSemanticNaming = true
  customTypeMapping = [
      "DateTime": "java.util.Date",
      "BigNumber": "java.math.BigInteger",
      "FunctionInput": "java.lang.String",
      "Long": "java.lang.Long",
      "HexString": "java.lang.String"
  ]
}

android {
  compileSdkVersion rootProject.ext.android["compileSdkVersion"]
  buildToolsVersion rootProject.ext.android["buildToolsVersion"]

  defaultConfig {
    applicationId "com.arcblock.sdk.demo"
    minSdkVersion rootProject.ext.android["minSdkVersion"]
    targetSdkVersion rootProject.ext.android["targetSdkVersion"]
    versionCode 1
    versionName version
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }

  signingConfigs {
    signingConfig {
      storeFile file('../crosscourse.jks')
      storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS :
          System.getenv("KEYSTORE_PASS")
      keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
      keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      zipAlignEnabled true
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.signingConfig
    }
    debug {
      signingConfig signingConfigs.signingConfig
      testCoverageEnabled = true
    }
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    ignore 'InvalidPackage', 'GoogleAppIndexingWarning', 'AllowBackup'
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation rootProject.ext.dependencies["appcompat-v7"]
  implementation rootProject.ext.dependencies["constraint-layout"]
  implementation rootProject.ext.dependencies["recyclerview-v7"]
  implementation rootProject.ext.dependencies["cardview-v7"]
  implementation rootProject.ext.dependencies["design"]
  implementation rootProject.ext.dependencies["BaseRecyclerViewAdapterHelper"]
  implementation rootProject.ext.dependencies["jsonviewer"]
  implementation rootProject.ext.dependencies["gson"]
  implementation (rootProject.ext.dependencies["utilcode"]){
    exclude module: 'gson'
  }
  implementation project(':absdkcorekit')
  //implementation rootProject.ext.dependencies["absdkcorekit"]

  //test
  testImplementation rootProject.ext.dependencies["junit"]
  androidTestImplementation rootProject.ext.dependencies["runner"]
  androidTestImplementation rootProject.ext.dependencies["espresso-core"]
  androidTestImplementation rootProject.ext.dependencies["espresso-intents"]
}
